dependencies {
    swaggerUI 'org.webjars:swagger-ui:4.1.3'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation "com.epages:restdocs-api-spec-mockmvc:${restdocsApiSpecVersion}"
}

openapi3 {
    servers = [
            { url = 'https://localhost:8080' },
            { url = 'https://api.ajaja.me' }
    ]
    title = 'ì˜¬í•´ë„ ì•„ì¢Œì¢ŒğŸ”¥'
    description = 'ë‰´ë°”ëŒìŠ¤ì˜ [ì˜¬í•´ë„ ì•„ì¢Œì¢ŒğŸ”¥] API ë¬¸ì„œì…ë‹ˆë‹¤.'
    version = 'v1'
    outputFileNamePrefix = "ajaja-api-spec"
}

var swaggerSource = generateDirectory("api-spec/${openapi3.outputFileNamePrefix}.${openapi3.format}").asFile
var duplicateInto = generateDirectory("resources/main/static/docs")

swaggerSources {
    ajaja { // task name will be generateSwaggerUIAjaja
        setInputFile(swaggerSource)
    }
}

tasks.withType(project.GenerateSwaggerUI).configureEach {
    dependsOn ':openapi3'
}

tasks.register("duplicateSwaggerUiIntoJar", Copy) {
    group 'custom'
    description 'duplicate Swagger UI which generated by task into jar'

    dependsOn ':generateSwaggerUIAjaja'
    from "${generateSwaggerUIAjaja.outputDir}"
    into duplicateInto

    tasks.resolveMainClassName.mustRunAfter ':duplicateSwaggerUiIntoJar'
}

bootJar {
    dependsOn ':duplicateSwaggerUiIntoJar'
}

def generateDirectory(String path) {
    return layout.buildDirectory.dir(path).get()
}
