plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
    id 'jacoco'
    id 'checkstyle'
    id 'com.epages.restdocs-api-spec' version "${restdocsApiSpecVersion}"
    id 'org.hidetake.swagger.generator' version "${swaggerGeneratorVersion}"
}

group = "${projectGroup}"
version = "${applicationVersion}"

jar.enabled = false
java.sourceCompatibility = "${javaVersion}"

apply from: 'gradle/jacoco.gradle'
apply from: 'gradle/lombok.gradle'
apply from: 'gradle/check-style.gradle'
apply from: 'gradle/test-support.gradle'
apply from: 'gradle/rest-docs.gradle'

dependencies {
    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-quartz'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Test
    testImplementation 'org.springframework.security:spring-security-test'

    // AWS - spring jcl logging conflict
    implementation 'com.amazonaws:aws-java-sdk-ses:1.12.580' exclude group: 'commons-logging', module: 'commons-logging'

    // Openfeign
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.0.4'

    // DB
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.h2database:h2'

    // Querydsl
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // Flyway
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'

    // Mapstruct
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    // Jasypt
    implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.4'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // Fixture-Monkey
    testImplementation 'com.navercorp.fixturemonkey:fixture-monkey-starter:1.0.0'

    // Embedded Redis
    testImplementation 'com.github.codemonstur:embedded-redis:1.3.0'

    // ahocorasick
    implementation 'org.ahocorasick:ahocorasick:0.6.3'
}

configurations {
    querydsl.extendsFrom compileClasspath
}

repositories {
    mavenCentral()
}

clean {
    delete file('src/main/generated')
}
